apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "org.ligi.survivalmanual"

        minSdkVersion 10

        targetSdkVersion 25
        versionCode 32
        versionName "3.2"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "SurvivalManual-$versionName"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "build", "fire"

    productFlavors {

        withFirebase {
            dimension "fire"
        }

        noFirebase {
            dimension "fire"
        }

        dev {
            dimension "build"
            minSdkVersion 21
        }

        prod {
            dimension "build"
        }
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'

        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    // ugly but the best workaround I found for getting the permission for spoon screenshot writing to SD-Card
    // http://stackoverflow.com/questions/25276537/how-do-i-change-android-permission-settings-when-running-tests
    sourceSets {
        debug {
            manifest.srcFile 'src/androidTest/AndroidManifest.xml'
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        warning 'MissingTranslation'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:preference-v7:$support_version"
    compile 'es.nitaur.markdown:txtmark:0.16'

    compile("com.chibatching.kotpref:kotpref:2.0.0") {
        exclude group: 'org.jetbrains.kotlin'
    }

    compile("com.chibatching.kotpref:initializer:2.0.0") {
        exclude group: 'org.jetbrains.kotlin'
    }


    compile "com.android.support:cardview-v7:$support_version"

    compile 'com.github.ligi:TouchImageView:2.1'
    compile ('com.github.ligi:ExtraCompats:0.4') {
        exclude group: 'org.jetbrains.kotlin'
    }

    compile 'com.github.ligi:snackengage:0.9'
    compile 'com.github.ligi:KAXT:0.15'

    compile 'com.github.hotchemi:permissionsdispatcher:2.3.1'
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:2.3.1'

    androidTestCompile "com.android.support:support-annotations:$support_version"
    androidTestCompile "com.android.support:appcompat-v7:$support_version"
    androidTestCompile "com.android.support:design:$support_version"

    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        // http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    androidTestCompile 'com.github.ligi:trulesk:0.18'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestCompile 'org.mockito:mockito-core:2.7.10'
    androidTestCompile 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'

    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile "com.android.support:support-annotations:$support_version"

    withFirebaseCompile "com.google.firebase:firebase-core:$firebase_version"
    withFirebaseCompile "com.google.firebase:firebase-crash:$firebase_version"
}

if (hasProperty("gms")) {
    apply plugin: 'com.google.gms.google-services'
}

spoon {
    debug = true
    grantAllPermissions = true
}

